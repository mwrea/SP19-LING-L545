Multichar_Symbols

%<n%>                ! Имя существительное
%<pl%>               ! Множественное число
%<gen%>              ! Genitive case
%<nom%>              ! Именительный падеж
%<ins%>              ! Творительный падеж
%{A%}                ! vowel harmony
%{Ă%}
%{м%}
%{э%}                ! front
%{а%}                ! back
%{л%}
%{с%} 
%<der_лӑх%>
%>                   ! Граница морфемы

LEXICON Root

Nouns ;

LEXICON CASES
%<nom%>: # ;
%<ins%>:%>п%{A%} # ;
%<gen%>:%>%{Ă%}н # ;


LEXICON PLURAL

             CASES ;
%<pl%>:%>се%{м%} CASES ;

LEXICON SUBST

PLURAL ;


LEXICON DER-N

%<der_лӑх%>:%>л%{Ă%}х SUBST "weight: 1.0" ;


LEXICON N

%<n%>: SUBST ;
%<n%>: DER-N ;

LEXICON NUM-DIGIT

%<num%>:%- CASES ;

LEXICON LAST-DIGIT

1:1%{э%}%{л%}    NUM-DIGIT ; ! "пӗр"
2:2%{с%}%{э%}    NUM-DIGIT ; ! "иккӗ"
3:3%{с%}%{э%}    NUM-DIGIT ; ! "виҫҫӗ"
4:4%{с%}%{а%}    NUM-DIGIT ; ! "тӑваттӑ"
5:5%{э%}%{с%}    NUM-DIGIT ; ! "пиллӗк"
6:6%{с%}%{а%}    NUM-DIGIT ; ! "улттӑ"
7:7%{с%}%{э%}    NUM-DIGIT ; ! "ҫиччӗ"
8:8%{э%}%{л%}    NUM-DIGIT ; ! "саккӑр"
9:9%{э%}%{л%}    NUM-DIGIT ; ! "тӑххӑр"

LEXICON LOOP

                 LAST-DIGIT ;
                 DIGITLEX ;

LEXICON DIGITLEX

%0:%0 LOOP ;
1:1   LOOP ;
2:2   LOOP ;
3:3   LOOP ;
4:4   LOOP ;
5:5   LOOP ;
6:6   LOOP ;
7:7   LOOP ;
8:8   LOOP ;
9:9   LOOP ;


LEXICON Nouns
патшалӑх:патшалӑх N ; ! "государство", "state"
урам:урам N ;     ! "улица", "street"
пахча:пахча N ;   ! "сад", "garden"
хула:хула N ;     ! "город", "city"
канаш:канаш N ;   ! "совет", "council"
тӗс:тӗс N ;       ! "вид", "aspect"
патша:патша N ;   ! "царь", "tsar"
куҫ:куҫ N ;       ! "глаз", "eye"
